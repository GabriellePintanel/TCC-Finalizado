plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.5'
    id 'io.spring.dependency-management' version '1.1.6'
    id "net.bytebuddy.byte-buddy-gradle-plugin" version "1.15.10"
}

group = 'local.grabrielle'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

configurations {
    byteBuddyAgent
}

dependencies {
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.mockito:mockito-inline:5.2.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testRuntimeOnly 'com.h2database:h2'

    byteBuddyAgent "net.bytebuddy:byte-buddy-agent:1.15.10"
}

configurations {
    configureEach {
        resolutionStrategy {
            eachDependency {
                if (requested.group == 'org.xmlunit' && requested.name == 'xmlunit-core') {
                    useTarget('org.xmlunit:xmlunit-core:2.10.0')
                }
                if (requested.group == 'net.bytebuddy' && requested.name == 'byte-buddy') {
                    useTarget('net.bytebuddy:byte-buddy:1.15.10')
                }
                if (requested.group == 'net.bytebuddy' && requested.name == 'byte-buddy-agent') {
                    useTarget('net.bytebuddy:byte-buddy-agent:1.15.10')
                }
            }
        }
    }
}

tasks.named('test') {
    useJUnitPlatform()
    jvmArgs("-javaagent:${configurations.byteBuddyAgent.singleFile}")
}
